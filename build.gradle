apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.1'
    resourcePrefix "commen__"	//这个随便填

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    compile files('libs/Parse-1.9.4.jar')
    compile files('libs/commons-codec-1.6.jar')
    compile files('libs/alipaySdk-20160516.jar')
    compile 'com.android.support:support-v4:23.+'
    compile 'com.android.support:recyclerview-v7:23.+'
    compile 'com.android.support:support-annotations:23.+'
    compile 'com.android.support:design:23.+'
    compile 'com.android.support:cardview-v7:23.+'
    compile 'com.android.support:appcompat-v7:23.+'
    //retrofit和rxjava
    compile 'com.squareup.retrofit2:retrofit:2.0.0-beta3'
    compile 'com.squareup.okhttp3:logging-interceptor:3.1.2'
    compile 'io.reactivex:rxjava:1.0.1'
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'com.squareup.retrofit2:converter-gson:2.0.0-beta4'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.0-beta4'
    compile 'com.flyco.tablayout:FlycoTabLayout_Lib:2.0.8@aar'
    //菜单栏
    compile 'com.android.support:multidex:1.0.0'
    //分包
    compile 'com.alibaba:fastjson:1.2.8'
    //json解析封装框架
    compile 'com.commit451:PhotoView:1.2.4'
    //图片缩放
    compile 'com.orhanobut:logger:1.13'
    //打印日志
    compile 'com.github.bumptech.glide:glide:3.6.1'
    //显示图片框架
    compile 'com.jakewharton:butterknife:7.0.1'
    //view注解
    compile 'com.github.clans:fab:1.6.4'
    //悬浮动画菜单
    compile 'cn.hugeterry.updatefun:updatefun:1.8.6'
    //fire.im自动更新
    compile 'com.umeng.analytics:analytics:latest.integration'
    //友盟统计

    compile files('libs/zxing.jar')


}

ext {
    isLibrary = true
    pomArtifactId = "Common"
    pomDescription = "Common of android picker"
}
def siteUrl = 'https://github.com/Superingxz/commen'      // 项目的主页
def gitUrl = 'https://github.com/Superingxz/commen.git'   // Git仓库的url
group = "com.mo.commen"                                        // Maven Group ID for the artifact，一般填你唯一的包名
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                // Add your description here
                name 'Android Commen' 	//项目描述
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'superingxz'		//填写的一些基本信息
                        name 'superingxz'
                        email '1803117759@qq.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    options.encoding = "utf-8"
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "commen"    //发布到JCenter上的项目名字
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}
